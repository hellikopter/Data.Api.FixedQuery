PREFIX : @schemaUri

CONSTRUCT {
    ?committee
        a ?class ;
        :groupStartDate ?startDate ;
        :groupName ?committeeName ;
    .

    [ :value ?firstLetter ]
}
WHERE {
    {
        SELECT *
        WHERE {
            ?committee
                a :ParliamentaryCommittee ;
                a ?class ;
            .

            OPTIONAL { ?committee :groupName ?committeeName . }
            OPTIONAL { ?committee :groupStartDate ?startDate . }
            FILTER NOT EXISTS { ?committee :groupEndDate ?endDate .}

            FILTER STRSTARTS(LCASE(?committeeName), LCASE(@initial))
        }
    }
    UNION {
        SELECT *
        WHERE {
            ?committee
                a :StatutoryCommittee ;
                a ?class ;
            .

            OPTIONAL { ?committee :groupName ?committeeName . }
            OPTIONAL { ?committee :groupStartDate ?startDate . }
            FILTER NOT EXISTS { ?committee :groupEndDate ?endDate . }

            FILTER STRSTARTS(LCASE(?committeeName), LCASE(@initial))
        }
    }
    UNION {
        SELECT DISTINCT ?firstLetter
        WHERE {
            {
                ?committee a :ParliamentaryCommittee .
                ?committee :groupName ?committeeName .
                BIND(UCASE(SUBSTR(?committeeName, 1, 1)) AS ?firstLetter)
            }
            UNION {
                ?committee a :StatutoryCommittee .
                ?committee :groupName ?committeeName .
                BIND(UCASE(SUBSTR(?committeeName, 1, 1)) AS ?firstLetter)
            }
        }
    }
}
