PREFIX : @schemaUri
CONSTRUCT {
    ?house
        a :House ;
        :houseName ?houseName .
    ?party
        a :Party ;
        :partyName ?partyName .
}
WHERE {
    BIND(@house_id as ?house)
    ?house
        a :House ;
        :houseName ?houseName .
    ?person a :Member .
    ?incumbency
        :parliamentaryIncumbencyHasMember ?person ;
        :parliamentaryIncumbencyStartDate ?incStartDate .
    OPTIONAL { ?incumbency :parliamentaryIncumbencyEndDate ?incumbencyEndDate . }
    ?incumbency :seatIncumbencyHasHouseSeat ?houseSeat .
    ?houseSeat :houseSeatHasHouse ?house .
    ?partyMembership
        :partyMembershipHasPartyMember ?person ;
        :partyMembershipHasParty ?party ;
        :partyMembershipStartDate ?pmStartDate .
    OPTIONAL { ?partyMembership :partyMembershipEndDate ?partyMembershipEndDate . }
    ?party :partyName ?partyName.
    BIND(COALESCE(?partyMembershipEndDate,now()) AS ?pmEndDate)
    BIND(COALESCE(?incumbencyEndDate,now()) AS ?incEndDate)
    FILTER (
        (?pmStartDate <= ?incStartDate && ?pmEndDate > ?incStartDate) ||
        (?pmStartDate >= ?incStartDate && ?pmStartDate < ?incEndDate)
    )
}
