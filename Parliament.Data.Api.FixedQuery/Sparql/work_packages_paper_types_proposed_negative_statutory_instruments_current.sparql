PREFIX : @schemaUri
PREFIX id: @instanceUri

CONSTRUCT {
  ?workPackagedThing 
    	a :WorkPackagedThing ;
    	:workPackagedThingName ?workPackagedThingName ;
    	:laidThingHasLaying ?laying . 	
  ?workPackage
    	a :WorkPackage ;
  		:workPackageHasWorkPackagedThing ?workPackagedThing ;
  		:workPackageHasProcedure ?procedure .
  ?laying
    	a :Laying ;
  		:layingDate ?layingDate .
  ?procedure
        a :Procedure ;
        :procedureName ?procedureName .
}
WHERE {
    {
    SELECT ?workPackage ?workPackagedThing WHERE {
      BIND(id:u5AUJb2q AS ?procedureStep1)
      BIND(id:hN1EDPLv AS ?procedureStep2)
      
      ?workPackagedThing a :ProposedNegativeStatutoryInstrumentPaper ;
  						:workPackagedThingHasWorkPackage ?workPackage .
  	
      FILTER NOT EXISTS { ?workPackage :workPackageHasBusinessItem/:businessItemHasProcedureStep ?procedureStep1 . }
      FILTER NOT EXISTS { ?workPackage :workPackageHasBusinessItem/:businessItemHasProcedureStep ?procedureStep2 . }
    }
  }	
  OPTIONAL { ?workPackagedThing :workPackagedThingName ?workPackagedThingName . }
  OPTIONAL { 
    ?workPackagedThing :laidThingHasLaying ?laying . 
  	?laying :layingDate ?layingDate . 
  }
  
  OPTIONAL {
    ?workPackage :workPackageHasProcedure ?procedure .
    
  OPTIONAL {
    ?procedure :procedureName ?procedureName . }
  }
}