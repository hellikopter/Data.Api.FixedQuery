PREFIX : @schemaUri
PREFIX id: @instanceUri

CONSTRUCT {
  ?procedureStep 
    a :ProcedureStep ;
  	:procedureStepName ?procedureStepName ;
  	:procedureStepDescription ?procedureStepDescription ;
  	:procedureStepHasBusinessItem ?businessItem .
  ?businessItem
    a :BusinessItem ;
    :businessItemDate ?businessItemDate .
  ?workPackage
    a :WorkPackage ;
    :workPackageHasWorkPackagedThing ?workPackagedThing ;
    :workPackageHasProcedure ?procedure ;
    :workPackageHasBusinessItem ?businessItem .
  ?workPackagedThing
    a :WorkPackagedThing ;
    :workPackagedThingName ?workPackagedThingName .
  ?procedure
    a :Procedure ;
    :procedureName ?procedureName .
}
WHERE {
  BIND(id:u5AUJb2q AS ?concludingStep1)
  BIND(id:hN1EDPLv AS ?concludingStep2)
  BIND(@procedure_step_id AS ?procedureStep)
       
  ?procedureStep a :ProcedureStep .
  OPTIONAL { ?procedureStep :procedureStepName ?procedureStepName . }
  OPTIONAL { ?procedureStep :procedureStepDescription ?procedureStepDescription . }
  
  OPTIONAL {
    ?procedureStep :procedureStepHasBusinessItem ?businessItem .
    ?businessItem :businessItemHasWorkPackage ?workPackage .
    
    OPTIONAL { ?businessItem :businessItemDate ?businessItemDate . }
  
    FILTER NOT EXISTS { ?workPackage :workPackageHasBusinessItem/:businessItemHasProcedureStep ?concludingStep1 . }
    FILTER NOT EXISTS { ?workPackage :workPackageHasBusinessItem/:businessItemHasProcedureStep ?concludingStep2 . }
  
    OPTIONAL {
      ?workPackage :workPackageHasProcedure ?procedure .
      OPTIONAL { ?procedure :procedureName ?procedureName . }
    } 
    
    OPTIONAL {
      ?workPackage :workPackageHasWorkPackagedThing ?workPackagedThing .
      OPTIONAL { ?workPackagedThing :workPackagedThingName ?workPackagedThingName . }
    }
  }
}