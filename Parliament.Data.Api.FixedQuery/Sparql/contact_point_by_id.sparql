PREFIX :@schemaUri
CONSTRUCT {
    ?contactPoint
        a :ContactPoint ;
        :email ?email ;
        :phoneNumber ?phoneNumber ;
        :faxNumber ?faxNumber ;
        :contactPointHasPostalAddress ?postalAddress ;
        :contactPointHasParliamentaryIncumbency ?incumbency .
    ?incumbency
        a :ParliamentaryIncumbency ;
        :parliamentaryIncumbencyHasMember ?person .
    ?postalAddress
        a :PostalAddress ;
        :postCode ?postCode ;
        :addressLine1 ?addressLine1 ;
        :addressLine2 ?addressLine2 ;
        :addressLine3 ?addressLine3 ;
        :addressLine4 ?addressLine4 ;
        :addressLine5 ?addressLine5 .
    ?person
        a :Person ;
        :personGivenName ?givenName ;
        :personFamilyName ?familyName ;
        <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?displayAs .
}
WHERE {
    BIND(@contact_point_id AS ?contactPoint )
    ?contactPoint a :ContactPoint .
    OPTIONAL{ ?contactPoint :email ?email . }
    OPTIONAL{ ?contactPoint :phoneNumber ?phoneNumber . }
    OPTIONAL{ ?contactPoint :faxNumber ?faxNumber . }
    OPTIONAL{
        ?contactPoint :contactPointHasPostalAddress ?postalAddress .
        OPTIONAL{ ?postalAddress :postCode ?postCode . }
        OPTIONAL{ ?postalAddress :addressLine1 ?addressLine1 . }
        OPTIONAL{ ?postalAddress :addressLine2 ?addressLine2 . }
        OPTIONAL{ ?postalAddress :addressLine3 ?addressLine3 . }
        OPTIONAL{ ?postalAddress :addressLine4 ?addressLine4 . }
        OPTIONAL{ ?postalAddress :addressLine5 ?addressLine5 . }
	}
    OPTIONAL{
        ?contactPoint :contactPointHasParliamentaryIncumbency ?incumbency .
        ?incumbency :parliamentaryIncumbencyHasMember ?person .
        OPTIONAL { ?person :personFamilyName ?familyName . }
        OPTIONAL { ?person :personGivenName ?givenName . }
        OPTIONAL { ?person <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?displayAs } .
	}
}
