PREFIX : @schemaUri

CONSTRUCT {
  ?group
    a :Group, :LayingBody ;
    :groupName ?groupName .


  ?laying
    a :Laying;
    :layingDate ?layingDate .

  ?laidThing
    a :LaidThing, ?class;
    :laidThingName ?laidThingName ;
    :laidThingHasLaying ?laying ;
    :workPackagedThingHasWorkPackage ?workPackage .

  ?workPackage
    a :WorkPackage;
    :workPackageHasProcedure ?procedure .

  ?procedure
    a :Procedure;
    :procedureName ?procedureName .
 }

WHERE {
  BIND (@group_id as ?group)

  ?group a :LayingBody .

  OPTIONAL { ?group :groupName ?groupName . }

  OPTIONAL { ?group :layingBodyHasLaying ?laying .
    OPTIONAL { ?laying :layingDate ?layingDate . }
    OPTIONAL { ?laying :layingHasLaidThing ?laidThing .
    	?laidThing a ?class .

      OPTIONAL { ?laidThing :laidThingName ?laidThingName . }
      OPTIONAL { ?laidThing :laidThingHasLaying ?laying . }
      OPTIONAL { ?laidThing :workPackagedThingHasWorkPackage ?workPackage .
		 ?workPackage :workPackageHasProcedure ?procedure .
		 ?procedure :procedureName ?procedureName .
      }
    }
  }
}
