PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : @schemaUri
CONSTRUCT {
	_:x :value ?firstLetter .
}
WHERE {
   SELECT DISTINCT ?firstLetter WHERE {
        BIND(@parliament_id AS ?parliament)
        BIND(@party_id AS ?party)
    	BIND(@house_id AS ?house)

        ?party a :Party .
        ?house a :House .
        ?parliament a :ParliamentPeriod ;
        			:parliamentPeriodHasSeatIncumbency ?seatIncumbency .
        ?seatIncumbency :parliamentaryIncumbencyHasMember ?person ;
                        :seatIncumbencyHasHouseSeat ?houseSeat ;
            			:parliamentaryIncumbencyStartDate ?seatIncumbencyStartDate.
        OPTIONAL { ?seatIncumbency :parliamentaryIncumbencyEndDate ?seatIncumbencyEndDate . }
        ?houseSeat :houseSeatHasHouse ?house .
        ?person :partyMemberHasPartyMembership ?partyMembership.
        ?partyMembership :partyMembershipHasParty ?party ;
                         :partyMembershipStartDate ?partyMembershipStartDate.
        OPTIONAL { ?partyMembership :partyMembershipEndDate ?partyMembershipEndDate . }

        BIND(xsd:dateTime(?partyMembershipEndDate) AS ?pmEndDateTime)
        BIND(xsd:dateTime(?seatIncumbencyEndDate) AS ?incEndDateTime)
        BIND(xsd:dateTime(?seatIncumbencyStartDate) AS ?incStartDate)
        BIND(xsd:dateTime(?partyMembershipStartDate) AS ?pmStartDate)

        BIND(COALESCE(?pmEndDateTime, now()) AS ?pmEndDate)
        BIND(COALESCE(?incEndDateTime, now()) AS ?incEndDate)
        FILTER(
        	(?pmStartDate <= ?incStartDate && ?pmEndDate > ?incStartDate) ||
        	(?pmStartDate >= ?incStartDate && ?pmStartDate < ?incEndDate)
		)

        ?person <http://example.com/A5EE13ABE03C4D3A8F1A274F57097B6C> ?listAs .
        BIND(ucase(SUBSTR(?listAs, 1, 1)) as ?firstLetter)
   }
}
