# In response to a request from Marc Adams (Senior Analyst & Mesh Mapping Lead @ the National Audit Offic) on 24/07/2018

PREFIX : @schemaUri

SELECT *
WHERE {
    ?formalBody
        :formalBodyStartDate ?formalBodyStartDate ;
    .
    ?formalBodyMembership
        :formalBodyMembershipHasFormalBody ?formalBody ;
        :formalBodyMembershipStartDate ?formalBodyMembershipStartDate ;
    .
    ?person
        :personHasFormalBodyMembership ?formalBodyMembership ;
        :partyMemberHasPartyMembership ?partyMembership ;
    .
    ?partyMembership
        :partyMembershipHasPartyMember ?person ;
        :partyMembershipStartDate ?partyMembershipStartDate ;
    .
    ?party
        :partyHasPartyMembership ?partyMembership ;
        :partyName ?partyName ;
    .

    OPTIONAL { ?formalBody :formalBodyName ?formalBodyName . }
    OPTIONAL { ?formalBody :formalBodyRemit ?formalBodyRemit . }
    OPTIONAL {
        ?formalBodyType
            :formalBodyTypeHasFormalBody ?formalBody ;
            :formalBodyTypeName ?formalBodyTypeName ;
        .
    }
    OPTIONAL { ?person :personGivenName ?personGivenName . }
    OPTIONAL { ?person :personOtherNames ?personOtherNames . }
    OPTIONAL { ?person :personFamilyName ?personFamilyName . }

    MINUS{?formalBody a :PastFormalBody . }
    MINUS{?formalBodyMembership a :PastFormalBodyMembership . }
    MINUS{?partyMembership a :PastPartyMembership . }
}
